// Original Hello C app by Francisco Garcia Collado, MIT Licence
// Translated to Oberon+ by Rochus Keller

module Hello
  import G := NAppWidgets, C := NAppCore, D := NAppDraw
var 
    window : *G.Window
    text : *G.TextView
    clicks : integer
    
  proc i_OnButton(app: *void; e: *C.Event)
    var msg: carray 1 of *C.String
  begin
    msg[0] := C.str_printf("Button click (%d)"+0ax, clicks) 
    G.textview_writef(text, C.tc(msg[0])) 
    C.str_destroy(msg)
    inc(clicks)
    // unref(e);
  end i_OnButton
  
  proc i_panel(): *G.Panel
  var
    panel: *G.Panel
    layout: *G.Layout
    label: *G.Label
    button: *G.Button
  begin
    panel := G.panel_create()
    layout := G.layout_create(1, 3)
    label := G.label_create()
    button := G.button_push()
    text := G.textview_create()
    G.label_text(label, "Hello!, I'm a label")
    G.button_text(button, "Click Me!")
    G.button_OnClick(button, C.listener_imp(nil,i_OnButton))
    G.layout_label(layout, label, 0, 0)
    G.layout_button(layout, button, 0, 1)
    G.layout_textview(layout, text, 0, 2)
    G.layout_hsize(layout, 0, 250)
    G.layout_vsize(layout, 2, 100)
    G.layout_margin(layout, 5)
    G.layout_vmargin(layout, 0, 5)
    G.layout_vmargin(layout, 1, 5)
    G.panel_layout(panel, layout)
    return panel
  end i_panel
  
  proc i_OnClose(app: *void; e: *C.Event)
  begin
    G.osapp_finish()
    //unref(app);
    //unref(e);
  end i_OnClose
  
  proc i_create()
    var
      panel: *G.Panel
  begin
    panel := i_panel()
    window := G.window_create(G.ekWNSTD)
    G.window_panel(window, panel)
    G.window_title(window, "Hello, World!")
    G.window_origin(window, D.v2df(500, 200))
    G.window_OnClose(window, C.listener_imp(nil,i_OnClose))
    G.window_show(window)
  end i_create

  proc i_destroy(item: *[]*void)
    var w: carray 1 of *G.Window
  begin
    w[0] := window
    G.window_destroy(w)
    window := nil
  end i_destroy
  
  proc i_update(app: *void; prtime: longreal; ctime: longreal)
  begin
  end i_update
  
begin
  G.osmain_imp(0, nil, nil, 0.0, i_create, nil, i_destroy, nil)
end Hello
